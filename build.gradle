plugins {
	id 'application'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'checkstyle'
}

sourceCompatibility = 1.8
version = '0.1.0'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
	version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

group = 'com.patchworkmc'
archivesBaseName = 'cursed-tool'

repositories {
	mavenCentral()

	maven {
		name 'patchwork'
		url 'https://dl.bintray.com/patchworkmc/Patchwork-Maven/'
	}
}

dependencies {
	implementation 'org.fusesource.jansi:jansi:1.18'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.10.0.pr3'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.0.pr3'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0.pr3'
	implementation 'com.patchworkmc:application-core:0.1.0'
}

checkstyle {
	configFile = rootProject.file("checkstyle.xml")
	toolVersion = '8.25'
}

application {
	mainClassName 'com.patchworkmc.cursed.CursedTool'
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

def runDir = file("run")

if(!runDir.exists() && !runDir.mkdirs()) {
	throw new IOException("Failed to create run directory ${runDir.getAbsolutePath()}")
}

run {
	workingDir runDir
}

jar {
	manifest {
		attributes(
				'Main-Class': 'com.patchworkmc.cursed.CursedTool'
		)
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}